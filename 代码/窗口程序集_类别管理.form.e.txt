.版本 2
.支持库 spec

.程序集 窗口程序集_类别管理
.程序集变量 色表, 哈希表_ASM


.子程序 _类别管理_创建完毕
    .局部变量 cnt, 整数型
    .局部变量 i, 整数型
    .局部变量 Color, 整数型
    .局部变量 Name, 文本型

    cnt ＝ EProject.取成员数 (“Type Info”)
    调试输出 (cnt)
    色表.创建 (#文本型, #整数型, , , )
    .变量循环首 (0, cnt － 1, 1, i)
        Name ＝ EProject.取成员 (“Type Info”, i).取文本 (“Name”)
        Color ＝ EProject.取成员 (“Type Info”, i).取整数 (“Color”)
        列表框1.插入项目 (0, Name, )
        色表.添加 (Name, Color)
    .变量循环尾 ()
    窗口_注册窗口热键 (取窗口句柄 (), , #回车键, , &确认键2, 假)
    

.子程序 _列表框1_列表项被选择
    .如果真 (列表框1.现行选中项 ＝ -1)
        颜色选择器1.可视 ＝ 假
        编辑框1.可视 ＝ 假
        按钮2.可视 ＝ 假
        返回 ()
    .如果真结束
    
    颜色选择器1.颜色 ＝ 色表.取值 (列表框1.取项目文本 (列表框1.现行选中项), )
    编辑框1.内容 ＝ 列表框1.取项目文本 (列表框1.现行选中项)
    颜色选择器1.可视 ＝ 真
    编辑框1.可视 ＝ 真
    按钮2.可视 ＝ 真
    

.子程序 _按钮1_被单击
    .局部变量 Ret, 文本型

    输入框 (“请输入新类别插入的名称”, “新建”, “新建类别”, Ret, , 类别管理)
    .如果真 (色表.查询 (Ret))
        信息框 (“该类别已创建”, #错误图标, “错误”, 类别管理)
        返回 ()
    .如果真结束
    色表.添加 (Ret, 汇编_取随机数 (#黑色, #白色))
    
    列表框1.现行选中项 ＝ 列表框1.插入项目 (列表框1.取项目数 (), Ret, )
    类别管理._列表框1_列表项被选择 ()
    

.子程序 _按钮2_被单击
    .如果真 (列表框1.现行选中项 ≠ -1)
        色表.删除 (列表框1.取项目文本 (列表框1.现行选中项))
        列表框1.删除项目 (列表框1.现行选中项)
        .如果真 (列表框1.现行选中项 ＝ -1)
            颜色选择器1.可视 ＝ 假
            编辑框1.可视 ＝ 假
            按钮2.可视 ＝ 假
        .如果真结束
        
    .如果真结束
    

.子程序 _类别管理_将被销毁
    .局部变量 类型名, 文本型, , "0"
    .局部变量 cnt, 整数型
    .局部变量 i, 整数型
    .局部变量 now, zyJsonValue

    EProject.清空 (“Type Info”)
    cnt ＝ 色表.取所有键 (类型名)
    .计次循环首 (cnt, i)
        now ＝ EProject.插入成员 (“Type Info”, 0, )
        now.置文本 (“Name”, 类型名 [i])
        now.置整数 (“Color”, 色表.取值 (类型名 [i], ))
    .计次循环尾 ()
    Tools.销毁 ()
    载入 (Tools, Layer1, 假)
    
    更新框色 ()
    

.子程序 更新框色
    .局部变量 组件句柄, 整数型
    .局部变量 cnt, 整数型
    .局部变量 i, 整数型
    .局部变量 buf, 外形框
    .局部变量 child, 整数型, , "0"
    .局部变量 label, 文本型

    组件句柄 ＝ 寻找组件 (Layer1, , “外形框”, , )
    cnt ＝ 取找到组件数目 (组件句柄)
    .变量循环首 (0, cnt － 1, 1, i)
        buf ＝ 取所找到组件 (组件句柄, i)
        窗口_枚举子窗口 (buf.取窗口句柄 (), child, )
        label ＝ 窗口_取控件内容 (child [1])
        调试输出 (label)
        .如果真 (色表.查询 (label) ＝ 假)
            buf.销毁 ()
            到循环尾 ()
        .如果真结束
        buf.线条颜色 ＝ 色表.取值 (label, )
    .变量循环尾 ()
    清除组件寻找句柄 (组件句柄)

.子程序 _颜色选择器1_颜色被改变
    色表.删除 (列表框1.取项目文本 (列表框1.现行选中项))
    色表.添加 (列表框1.取项目文本 (列表框1.现行选中项), 颜色选择器1.颜色)

.子程序 _编辑框1_内容被改变
    色表.删除 (列表框1.取项目文本 (列表框1.现行选中项))
    列表框1.置项目文本 (列表框1.现行选中项, 编辑框1.内容)
    色表.添加 (编辑框1.内容, 颜色选择器1.颜色)
    

.子程序 确认键2
    销毁 ()
    

