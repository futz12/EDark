.版本 2
.支持库 iext
.支持库 spec

.程序集 窗口程序集_Main, , 公开
.程序集变量 图表, 哈希表_ASM

.程序集变量 上一张图片, 文本型

.子程序 _Main_创建完毕
    图片框1.图片 ＝ #背景图
    
    Main.标题 ＝ “EDarkTool ” ＋ #版本号 ＋ “ - 当前项目：” ＋ EProject.取文本 (“Project Name”) ＋ “ -  项目类型：” ＋ EProject.取文本 (“Project Type”)
    st.置文本 (0, #版本号)
    LOG (“项目 ” ＋ EProject.取文本 (“Project Name”) ＋ “ 导入成功，项目包含” ＋ 到文本 (EProject.取成员数 (“Img Info”)) ＋ “张图片数据，共有” ＋ 到文本 (EProject.取成员数 (“Type Info”)) ＋ “个类型”)
    buf_OnDraw ＝ 假
    载入 (Layer1, Main, 假)
    载入 (Tools, Main, 假)
    Main.调整层次 (#顶层)
    Layer1.调整层次 (#顶层)
    Tools.调整层次 (#顶层)
    
    图表.创建 (#文本型, #文本型, , , )
    
    读入图片组 ()
    未闻花名_窗口闪烁优化 (Layer1.取窗口句柄 (), 1)
    

.子程序 LOG, , ,  
    .参数 文本, 文本型

    调试输出 (文本)
    st.置文本 (1, 文本)

.子程序 _Main_尺寸被改变
    .局部变量 po_, 精易_坐标

    列表框1.高度 ＝ Main.高度 － 90
    图片框1.高度 ＝ Main.高度 － 100
    图片框1.宽度 ＝ Main.宽度 － 170
    po_ ＝ 窗口_取控件坐标 (图片框1.取窗口句柄 ())
    Layer1.左边 ＝ po_.横
    Layer1.顶边 ＝ po_.纵
    Layer1.高度 ＝ Main.高度 － 100
    Layer1.宽度 ＝ Main.宽度 － 170
    Tools.可视 ＝ 假
    

.子程序 _新建项目_被选择
    销毁 ()
    .如果真 (EProject.创建 (#Demo模板, , , ) ＝ 假)
        信息框 (“项目载入失败，请确认项目文件是否合法”, #错误图标, “错误”, )
        返回 ()
    .如果真结束
    载入 (Main, , 假)
    

.子程序 _打开项目_被选择
    .局部变量 project_file, 文本型
    .局部变量 project_bin, 文本型

    销毁 ()
    project_file ＝ 对话框_打开文件 (取窗口句柄 (), , “请选择要打开的项目”, “*.edark”, )
    project_bin ＝ 到文本 (读入文件 (project_file))
    .如果真 (EProject.创建 (project_bin, , , ) ＝ 假)
        信息框 (“项目载入失败，请确认项目文件是否合法”, #错误图标, “错误”, )
        返回 ()
    .如果真结束
    载入 (Main, , 假)
    

.子程序 _图片框1_鼠标左键被按下, 逻辑型
    .参数 横向位置, 整数型
    .参数 纵向位置, 整数型
    .参数 功能键状态, 整数型

    SetCapture (图片框1.取窗口句柄 ())
    
    Tools.可视 ＝ 假
    Tools.编辑框1.获取焦点 ()
    复制窗口组件 (Layer1.外形框1, buf_组件)
    复制窗口组件 (Layer1.标签1, buf_组件2)
    buf_组件2.置父窗口 (buf_组件)
    
    buf_组件.左边 ＝ 横向位置
    buf_组件.顶边 ＝ 纵向位置
    
    buf_组件.调整层次 (#顶层)
    
    buf_组件.可视 ＝ 真
    buf_组件2.可视 ＝ 真
    
    buf_OnDraw ＝ 真

.子程序 _图片框1_鼠标位置被移动, 逻辑型
    .参数 横向位置, 整数型
    .参数 纵向位置, 整数型
    .参数 功能键状态, 整数型
    .局部变量 buf, 外形框

    .如果真 (buf_OnDraw)
        buf_组件.宽度 ＝ 横向位置 － buf_组件.左边
        buf_组件.高度 ＝ 纵向位置 － buf_组件.顶边
        .如果真 (buf_组件.宽度 ≤ 64)
            buf_组件.宽度 ＝ 64
        .如果真结束
        .如果真 (buf_组件.高度 ≤ 64)
            buf_组件.高度 ＝ 64
        .如果真结束
        返回 (真)
    .如果真结束
    .如果真 (是否已创建 (top))
        top.填充颜色 ＝ #白色
        top ＝ buf
    .如果真结束
    
    

.子程序 _图片框1_鼠标左键被放开, 逻辑型
    .参数 横向位置, 整数型
    .参数 纵向位置, 整数型
    .参数 功能键状态, 整数型
    .局部变量 po_, 精易_坐标

    ReleaseCapture ()
    
    .如果真 (是否已创建 (buf_组件) ＝ 假)
        返回 ()
    .如果真结束
    .如果真 (buf_OnDraw)
        .如果真 (buf_组件.宽度 ≤ 64)
            buf_组件.宽度 ＝ 64
        .如果真结束
        .如果真 (buf_组件.高度 ≤ 64)
            buf_组件.高度 ＝ 64
        .如果真结束
        
    .如果真结束
    buf_OnDraw ＝ 假
    
    Tools.可视 ＝ 真
    po_ ＝ 窗口_取控件坐标 (buf_组件.取窗口句柄 ())
    Tools.左边 ＝ po_.横
    Tools.顶边 ＝ po_.纵 ＋ buf_组件.高度

.子程序 _Main_位置被改变
    .局部变量 po_, 精易_坐标

    po_ ＝ 窗口_取控件坐标 (图片框1.取窗口句柄 ())
    Layer1.左边 ＝ po_.横
    Layer1.顶边 ＝ po_.纵
    Tools.可视 ＝ 假
    

.子程序 _Main_将被销毁
    

.子程序 _Main_获得焦点
    Tools.可视 ＝ 假

.子程序 _类别设置_被选择
    载入 (类别管理, Layer1, 真)
    

.子程序 _保存项目_被选择
    .局部变量 data, 文本型

    保存框信息到项目 ()
    .如果真 (EProject_File ＝ “”)
        EProject_File ＝ 对话框_另存文件 (取窗口句柄 (), , “请选择要打开的项目”, , “EDark项目文件”, , )
    .如果真结束
    data ＝ EProject.到文本 (, , , )
    .如果真 (写到文件 (EProject_File, 到字节集 (data)) ＝ 假)
        信息框 (“写出失败”, #错误图标, “错误”, Main)
    .如果真结束
    保存框信息到项目 ()

.子程序 读入图片组
    .局部变量 list, zyJsonValue
    .局部变量 cnt, 整数型
    .局部变量 i, 整数型
    .局部变量 img, zyJsonValue

    列表框1.清空 ()
    list ＝ EProject.选择 (“Img Info”)
    cnt ＝ list.取成员数 ()
    .变量循环首 (0, cnt － 1, 1, i)
        img ＝ list.取成员 (, i)
        图表.添加 (img.取文本 (“Path”), img.到文本 (“Rect”, , , ))
        调试输出 (img.到文本 (“Rect”, , , ))
        列表框1.插入项目 (列表框1.取项目数 (), img.取文本 (“Path”), )
    .变量循环尾 ()

.子程序 载入图片到图片框, , , 必须装载了
    .参数 图片地址, 文本型
    .局部变量 bmp, 字节集

    .局部变量 坐标地址, zyJsonDocument
    .局部变量 cnt, 整数型
    .局部变量 i, 整数型

    bmp ＝ 图片_转换1 (读入文件 (图片地址), 1, 100)
    .如果真 (取字节集长度 (bmp) ＝ 0)
        信息框 (“图片：” ＋ 图片地址 ＋ “读入失败”, #错误图标, “错误”, )
        返回 ()
    .如果真结束
    图片框1.图片 ＝ bmp
    清空框 ()
    
    坐标地址.创建 (图表.取值 (图片地址, ), , , )
    
    cnt ＝ 坐标地址.取成员数 ()
    
    .变量循环首 (0, cnt － 1, 1, i)
        复制窗口组件 (Layer1.外形框1, buf_组件)
        复制窗口组件 (Layer1.标签1, buf_组件2)
        buf_组件2.置父窗口 (buf_组件)
        
        buf_组件.左边 ＝ 坐标地址.取成员 (, i).取整数 (“x”)
        buf_组件.顶边 ＝ 坐标地址.取成员 (, i).取整数 (“y”)
        
        buf_组件.宽度 ＝ 坐标地址.取成员 (, i).取整数 (“w”)
        buf_组件.高度 ＝ 坐标地址.取成员 (, i).取整数 (“h”)
        
        buf_组件.调整层次 (#顶层)
        
        buf_组件2.标题 ＝ 坐标地址.取成员 (, i).取文本 (“type”)
        
        buf_组件.可视 ＝ 真
        buf_组件2.可视 ＝ 真
    .变量循环尾 ()
    
    更新框色Tools ()

.子程序 _列表框1_列表项被选择
    .如果真 (列表框1.现行选中项 ＝ -1)
        保存框信息到哈希表 ()
        清空框 ()
        图片框1.图片 ＝ #背景图
        图片框1.禁止 ＝ 真
        上一张图片 ＝ “”
        返回 ()
    .如果真结束
    
    保存框信息到哈希表 ()
    载入图片到图片框 (列表框1.取项目文本 (列表框1.现行选中项))
    上一张图片 ＝ 列表框1.取项目文本 (列表框1.现行选中项)
    
    图片框1.禁止 ＝ 假
    

.子程序 保存框信息到哈希表
    .局部变量 坐标地址, zyJsonDocument

    .局部变量 组件句柄, 整数型
    .局部变量 cnt, 整数型
    .局部变量 i, 整数型
    .局部变量 buf, 外形框
    .局部变量 item, zyJsonValue
    .局部变量 child, 整数型, , "0"

    .如果真 (上一张图片 ＝ “”)
        返回 ()
    .如果真结束
    坐标地址.创建 (, , , )
    组件句柄 ＝ 寻找组件 (Layer1, , “外形框”, , )
    cnt ＝ 取找到组件数目 (组件句柄)
    .变量循环首 (0, cnt － 1, 1, i)
        buf ＝ 取所找到组件 (组件句柄, i)
        item ＝ 坐标地址.插入成员 (, i, )
        窗口_枚举子窗口 (buf.取窗口句柄 (), child, )
        调试输出 (窗口_取标题 (child [1]))
        item.置文本 (“type”, 窗口_取标题 (child [1]))
        item.置整数 (“x”, buf.左边)
        item.置整数 (“y”, buf.顶边)
        item.置整数 (“w”, buf.宽度)
        item.置整数 (“h”, buf.高度)
    .变量循环尾 ()
    清除组件寻找句柄 (组件句柄)
    图表.添加 (上一张图片, 坐标地址.到文本 (, , , ))
    

.子程序 清空框
    .局部变量 组件句柄, 整数型
    .局部变量 cnt, 整数型
    .局部变量 i, 整数型
    .局部变量 buf, 外形框

    组件句柄 ＝ 寻找组件 (Layer1, , “外形框”, , )
    cnt ＝ 取找到组件数目 (组件句柄)
    .变量循环首 (0, cnt － 1, 1, i)
        buf ＝ 取所找到组件 (组件句柄, i)
        buf.销毁 ()
    .变量循环尾 ()
    清除组件寻找句柄 (组件句柄)
    

.子程序 保存框信息到项目
    .局部变量 img, zyJsonValue
    .局部变量 path, 文本型, , "0"
    .局部变量 cnt, 整数型
    .局部变量 i, 整数型
    .局部变量 item, zyJsonValue

    EProject.清空 (“Img Info”)
    img ＝ EProject.选择 (“Img Info”)
    cnt ＝ 图表.取所有键 (path)
    .变量循环首 (0, cnt － 1, 1, i)
        item ＝ img.插入成员 (, i, )
        item.置文本 (“Path”, path [i ＋ 1])
        item.置JSON (“Rect”, 图表.取值 (path [i ＋ 1], ), )
    .变量循环尾 ()

.子程序 _列表框1_鼠标右键被按下, 逻辑型
    .参数 横向位置, 整数型
    .参数 纵向位置, 整数型
    .参数 功能键状态, 整数型

    弹出菜单 (项目操作, , )
    

.子程序 _删除_被选择
    .局部变量 path, 文本型

    .如果真 (列表框1.现行选中项 ≠ -1)
        path ＝ 列表框1.取项目文本 (列表框1.现行选中项)
        图表.删除 (path)
        列表框1.删除项目 (列表框1.现行选中项)
        .如果真 (列表框1.现行选中项 ＝ -1)
            图片框1.图片 ＝ #背景图
            图片框1.禁止 ＝ 真
            上一张图片 ＝ “”
            清空框 ()
        .如果真结束
        
    .如果真结束
    

.子程序 _插入图片_被选择
    .局部变量 path, 文本型
    .局部变量 addr, 整数型

    path ＝ 对话框_打开文件 (取窗口句柄 (), , “请选择要插入的图片”, , )
    .如果真 (path ＝ “”)
        返回 ()
    .如果真结束
    addr ＝ 选择 (列表框1.现行选中项 ＝ -1, 列表框1.取项目数 (), 列表框1.现行选中项)
    列表框1.现行选中项 ＝ 列表框1.插入项目 (addr, path, )
    图表.添加 (path, “[]”)
    Main._列表框1_列表项被选择 ()
    

.子程序 _Main_滚轮被滚动, 逻辑型
    .参数 滚动距离, 整数型
    .参数 功能键状态, 整数型

    .如果真 (是否已创建 (类别管理))
        返回 ()
    .如果真结束
    列表框1.现行选中项 ＝ 列表框1.现行选中项 － 滚动距离
    Tools.可视 ＝ 假
    Main._列表框1_列表项被选择 ()

.子程序 _列表框1_滚轮被滚动, 逻辑型
    .参数 滚动距离, 整数型
    .参数 功能键状态, 整数型

    .如果真 (是否已创建 (类别管理))
        返回 ()
    .如果真结束
    
    列表框1.现行选中项 ＝ 列表框1.现行选中项 － 滚动距离
    Tools.可视 ＝ 假
    Main._列表框1_列表项被选择 ()
    

.子程序 _图片框1_滚轮被滚动, 逻辑型
    .参数 滚动距离, 整数型
    .参数 功能键状态, 整数型

    .如果真 (是否已创建 (类别管理))
        返回 ()
    .如果真结束
    
    列表框1.现行选中项 ＝ 列表框1.现行选中项 － 滚动距离
    Tools.可视 ＝ 假
    Main._列表框1_列表项被选择 ()
    
    

.子程序 _导入图片_被选择
    _插入图片_被选择 ()
    

.子程序 _导入文件夹_被选择
    .局部变量 Path, 文本型
    .局部变量 File, 文本型, , "0"
    .局部变量 cnt, 整数型
    .局部变量 i, 整数型
    .局部变量 path, 文本型

    保存框信息到哈希表 ()
    
    Path ＝ 对话框_打开文件夹 (“请输入要导入的文件夹”, Main.取窗口句柄 ())
    .如果真 (Path ＝ “”)
        返回 ()
    .如果真结束
    文件_枚举 (Path, “*.jpg|*.jpge|*.png|*.bmp|*.gif|*.tiff”, File, 真, , 真)
    cnt ＝ 取数组成员数 (File)
    .计次循环首 (cnt, i)
        列表框1.插入项目 (0, File [i], )
        图表.添加 (File [i], “[]”)
    .计次循环尾 ()

